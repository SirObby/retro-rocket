REM *** Retro Rocket BASIC Shell ***

PROMPT$ = "ROCKETSH"
PATH$ = "/programs"
VERSION$ = "2.0"

PRINT ""
PRINT "Welcome to ";
COLOR 14
PRINT "Retro Rocket ";
COLOR 7
PRINT "BASIC shell."
PRINT ""

REPEAT
	PROCdisplayPrompt
	INPUT in$
	IF in$ > "" THEN PROCprocessCommand(in$)
UNTIL in$ = "END"
END

DEF PROCdisplayPrompt
	COLOR 14
	PRINT PROMPT$;
	COLOR 7
	PRINT "> ";
RETPROC

DEF PROCprocessCommand(inp$)
	spaceidx = INSTR(inp$, " ")
	command$ = ""
	IF spaceidx > 0 THEN command$ = LEFT$(inp$, spaceidx - 1)
	IF spaceidx < 1 THEN command$ = inp$
	GLOBAL ARGS$ = MID$(inp$, spaceidx, LEN(inp$))
	builtin = FALSE
	PROCbuiltins
	IF builtin = TRUE THEN RETPROC
	FH = OPENIN(PATH$ + "/" + command$)
	IF FH < 0 THEN PROCevalBasic
	IF FH > 0-1 THEN PROCrunCommand(command$)
RETPROC

DEF PROCbuiltins
	IF command$ = "chdir" THEN PROCbuiltinChdir
	IF command$ = "cd" THEN PROCbuiltinChdir
	IF command$ = "run" THEN PROCbuiltinRun
	IF command$ = "version" THEN PROCversion
RETPROC

DEF PROCversion
	PRINT "┌─────────────────────────────────────────────────────┐"
	PRINT "│ ";
	COLOR 14
	PRINT "rocketsh ";
	COLOR 7
	PRINT "Retro Rocket Shell version "; VERSION$; "             │"
	PRINT "├─────────────────────────────────────────────────────┤"
	PRINT "│ Licensed under the Apache 2.0 License.              │"
	PRINT "│ (C) Craig Edwards, Brainbox.cc 2012-2023            │"
	PRINT "└─────────────────────────────────────────────────────┘"
	builtin = TRUE
RETPROC

DEF PROCbuiltinChdir
	EVAL "CHDIR " + CHR$(34) + ARGS$ + CHR$(34)
	builtin = TRUE
RETPROC

DEF PROCbuiltinRun
	EVAL "CHAIN " + CHR$(34) + ARGS$ + CHR$(34)
	builtin = TRUE
RETPROC

DEF PROCevalBasic
	EVAL inp$
RETPROC

DEF PROCrunCommand(cmd$)
	CLOSE FH
	CHAIN PATH$ + "/" + cmd$
RETPROC
